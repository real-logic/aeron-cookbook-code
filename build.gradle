/*
 * Copyright 2019-2020 Shaun Laurens.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'maven-publish'
}

defaultTasks 'clean', 'build', 'test'

def cookbookSampleGroup = 'com.aeroncookbook'
def cookbookSampleVersion = '0.1-SNAPSHOT'
def javaVersion = JavaVersion.VERSION_11
def Properties props = new Properties()
def propFile = file('eider.properties')
def ghp_eider_username;
def ghp_eider_token;
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))
    if (props != null && props.containsKey('gpr.usr')) {
        ghp_eider_username = props['gpr.usr']
    } else {
        ghp_eider_username = System.getenv("GITHUB_ACTOR")
    }

    if (props != null && props.containsKey('gpr.key')) {
        ghp_eider_token = props['gpr.key']
    } else {
        ghp_eider_token = System.getenv("GITHUB_TOKEN")
    }
} else {
    ghp_eider_username = System.getenv("GPR_USER")
    ghp_eider_token = System.getenv("GPR_API_KEY")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    group cookbookSampleGroup
    version cookbookSampleVersion

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'java.base/java.net=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    compileTestJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'java.base/java.net=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
        options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    repositories {
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/eleventy7/eider")
            credentials {
                username = ghp_eider_username
                password = ghp_eider_token
            }
        }
    }

    dependencies {
        implementation group: 'org.agrona', name: 'agrona', version: '1.6.0'
        implementation group: 'io.aeron', name: 'aeron-all', version: '1.29.0'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.34'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    }

    tasks.withType(Checkstyle) {
        checkstyle {
            configFile = "${rootDir}/config/checks.xml" as File
            configProperties = ["suppressionFile": "${rootDir}/config/suppressions.xml"]
            toolVersion = '8.32'
        }
    }

    test {
        useJUnitPlatform()
        jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
        jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')

        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

project('ipc-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    dependencies {
        implementation group: 'org.agrona', name: 'agrona', version: '1.6.0'
        implementation group: 'io.aeron', name: 'aeron-all', version: '1.29.0'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.34'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    }
}

project('agrona') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    dependencies {
        implementation group: 'org.agrona', name: 'agrona', version: '1.6.0'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.34'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    }
}

project('theory') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.34'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    }
}

project('archive-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
}

project('cluster-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    def folder = "../eider-spec/build/generated/sources/annotationProcessor/java/main/"
    def folderDest = "${projectDir}/src/main/generated/"

    configurations {
        generatedEider
    }

    sourceSets {
        generatedEider {
            java {
                folderDest
            }
        }
        main {
            java {
                srcDirs = ['src/main/java', folderDest]
            }
        }
    }

    task copyGeneratedFiles(type: Copy) {
        from folder
        into folderDest
    }

    task clearGeneratedFiles(type: Delete) {
        delete folderDest
    }

    clearGeneratedFiles.dependsOn(copyGeneratedFiles)
    copyGeneratedFiles.dependsOn(':eider-spec:build')

    dependencies {
        generatedEider "org.agrona:agrona:1.6.0"
        testImplementation "org.mockito:mockito-junit-jupiter:3.4.4"
        testImplementation "org.mockito:mockito-inline:3.4.4"
    }

    compileJava {
        dependsOn copyGeneratedFiles
    }
}

project('aeron-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    def folder = "../eider-spec/build/generated/sources/annotationProcessor/java/main/"
    def folderDest = "${projectDir}/src/main/generated/"

    configurations {
        generatedEider
    }

    sourceSets {
        generatedEider {
            java {
                folderDest
            }
        }
        main {
            java {
                srcDirs = ['src/main/java', folderDest]
            }
        }
    }

    task copyGeneratedFiles(type: Copy) {
        from folder
        into folderDest
    }

    task clearGeneratedFiles(type: Delete) {
        delete folderDest
    }

    clearGeneratedFiles.dependsOn(copyGeneratedFiles)
    copyGeneratedFiles.dependsOn(':eider-spec:build')

    dependencies {
        generatedEider "org.agrona:agrona:1.6.0"
    }

    compileJava {
        dependsOn copyGeneratedFiles
    }
}


project(':sbe-protocol') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    dependencies {
        implementation "org.agrona:agrona:1.6.0"
        implementation "uk.co.real-logic:sbe-tool:1.19.0"
        testImplementation sourceSets.generated.output
    }

    compileGeneratedJava {
        dependsOn 'generateMessages'
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        options.deprecation = true
        classpath += sourceSets.main.runtimeClasspath
    }

    task generateMessages(type: JavaExec) {
        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = sourceSets.main.runtimeClasspath
        systemProperties('sbe.output.dir': generatedDir,
                'sbe.target.language': 'Java',
                'sbe.validation.stop.on.error': 'true',
                'sbe.validation.xsd': 'src/main/resources/sbe/sbe.xsd')
        args = [
                'src/main/resources/messages.xml'
        ]
    }

    jar.dependsOn compileGeneratedJava
}

project(':eider-spec') {
    apply plugin: 'java-library'

    configurations {
        genCompile
    }

    def generatedDir = file("${buildDir}/generated")
    sourceSets {
        gen {
            java {
                srcDir generatedDir
            }
        }
        main {
            java {
                srcDirs = ['src/main/java', generatedDir]
            }
        }
    }

    dependencies {
        implementation "org.agrona:agrona:1.6.0"
        genCompile "org.agrona:agrona:1.6.0"
        implementation group: 'io.eider', name: 'eider-processor', version: '0.27-SNAPSHOT'
        testImplementation group: 'io.eider', name: 'eider-processor', version: '0.27-SNAPSHOT'
        annotationProcessor group: 'io.eider', name: 'eider-processor', version: '0.27-SNAPSHOT'
        testImplementation sourceSets.gen.output
    }

}

project('sbe-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    dependencies {
        implementation project(path: ':sbe-protocol')
    }

    idea {
        dependencies {
            implementation project(':sbe-protocol').sourceSets.generated.output
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$project.projectDir/pom.xml")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'aeron-cookbook'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Aeron Cookbook'
                description = 'Dependency Tracking Only'
                url = 'http://www.aeroncookbook.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '6.4.1'
    distributionType = 'ALL'
}